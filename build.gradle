buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

plugins {
    id "net.ltgt.apt" version "0.10"
}

apply plugin: 'info.solidsoft.pitest'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:all"
javadoc.options.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {

    compile 'com.google.dagger:dagger:2.11'
    apt 'com.google.dagger:dagger-compiler:2.11'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.mockito:mockito-core:2.10.0'
}

tasks.withType(JavaCompile) {
    options.incremental = true
}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

mainClassName = 'ee.larseckart.logstats.Main'

jar {
    manifest {
        attributes(
                'Main-Class': 'ee.larseckart.logstats.Main'
        )
    }
}

pitest {
    targetClasses = ['ee.larseckart.*']
    testSourceSets = [sourceSets.test,]
    pitestVersion = "1.1.5"
    threads = 4
    outputFormats = ['HTML']
    enableDefaultIncrementalAnalysis = true
}

task histogram(type: JavaExec, dependsOn: 'classes') {
    classpath = sourceSets.main.runtimeClasspath

    main = 'ee.larseckart.logstats.ui.HourlyCountHistogram'

    // arguments to pass to the application
    args 'timing.log'
}
